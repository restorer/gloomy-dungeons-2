// org.zamedev.gloomydungeons2.fullnfree

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:0.11.1'
    }
}

apply plugin: 'android'

allprojects {
    tasks.withType(Compile) {
        options.compilerArgs << '-Xlint:all'
        options.compilerArgs << '-Xlint:-deprecation'
        options.compilerArgs << '-Xlint:-unchecked'
    }
}

android {
    compileSdkVersion Integer.parseInt(project.ANDROID_BUILD_SDK_VERSION)
    buildToolsVersion project.ANDROID_BUILD_TOOLS_VERSION
    // useLibrary 'org.apache.http.legacy'

    defaultConfig {
        minSdkVersion Integer.parseInt(project.ANDROID_BUILD_MIN_SDK_VERSION)
        targetSdkVersion Integer.parseInt(project.ANDROID_BUILD_TARGET_SDK_VERSION)

        versionName project.VERSION_NAME
        versionCode Integer.parseInt(project.VERSION_CODE)

        ndk {
            moduleName 'librenderer'
            cFlags '-Werror'
            ldLibs 'log', 'GLESv1_CM'
            // abiFilters 'armeabi-v7a', 'armeabi', 'x86'
        }
    }

    if (new File('tools/signing.properties').exists()) {
        Properties props = new Properties()
        props.load(new FileInputStream(file('tools/signing.properties')))

        signingConfigs {
            release {
                storeFile file('tools/signing.keystore')
                keyAlias props['keyAlias']
                storePassword props['storePassword']
                keyPassword props['keyPassword']
            }
        }

        buildTypes {
            release {
                signingConfig signingConfigs.release
            }
        }
    }

    buildTypes {
        release {
            runProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt'
        }
    }

    flavorDimensions 'package', 'gplay', 'zeemote', 'langko'

    productFlavors {
        forgplay {
            flavorDimension 'package'
            applicationId 'org.zamedev.gloomydungeons2.fullnfree'
        }

        forfdroid {
            flavorDimension 'package'
            applicationId 'org.zamedev.gloomydungeons2.opensource'
        }

        withgplay {
            flavorDimension 'gplay'
        }

        woutgplay {
            flavorDimension 'gplay'
        }

        withzeemote {
            flavorDimension 'zeemote'
        }

        woutzeemote {
            flavorDimension 'zeemote'
        }

        withko {
            flavorDimension 'langko'
        }

        woutko {
            flavorDimension 'langko'
        }
    }
}

dependencies {
    compile project(':libraries:holoeverywhere')
    compile project(':libraries:holoeverywhere-preferences')
    compile fileTree(dir: 'src/main/libs', include: ['*.jar'])
    withgplayCompile project(':libraries:facebook')
    withgplayCompile fileTree(dir: 'src/withgplay/libs', include: ['*.jar'])
    withzeemoteCompile fileTree(dir: 'src/withzeemote/libs', include: ['*.jar'])
}
