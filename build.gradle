// org.zamedev.gloomydungeons2.fullnfree

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.3'
    }
}

apply plugin: 'android'

allprojects {
    tasks.withType(JavaCompile) {
        options.compilerArgs << '-Xlint:all'
        options.compilerArgs << '-Xlint:-deprecation'
        options.compilerArgs << '-Xlint:-unchecked'
    }
}

android {
    compileSdkVersion 22
    buildToolsVersion '25.0.2'
    // useLibrary 'org.apache.http.legacy'

    defaultConfig {
        minSdkVersion Integer.parseInt(project.ANDROID_BUILD_MIN_SDK_VERSION)
        targetSdkVersion Integer.parseInt(project.ANDROID_BUILD_TARGET_SDK_VERSION)

        versionName project.VERSION_NAME
        versionCode Integer.parseInt(project.VERSION_CODE)

        ndk {
            moduleName 'librenderer'
            cFlags '-Werror'
            ldLibs 'log', 'GLESv1_CM'
            // abiFilters 'armeabi-v7a', 'armeabi', 'x86'
        }
    }

    if (new File('tools/signing.properties').exists()) {
        Properties props = new Properties()
        props.load(new FileInputStream(file('tools/signing.properties')))

        signingConfigs {
            release {
                storeFile file('tools/signing.keystore')
                keyAlias props['keyAlias']
                storePassword props['storePassword']
                keyPassword props['keyPassword']
            }
        }

        buildTypes {
            release {
                signingConfig signingConfigs.release
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt'
        }
    }

    flavorDimensions 'package', 'gplay', 'zeemote', 'langko'

    productFlavors {
        forgplay {
            dimension 'package'
            applicationId 'org.zamedev.gloomydungeons2.fullnfree'
        }

        forfdroid {
            dimension 'package'
            applicationId 'org.zamedev.gloomydungeons2.opensource'
        }

        withgplay {
            dimension 'gplay'
        }

        woutgplay {
            dimension 'gplay'
        }

        withzeemote {
            dimension 'zeemote'
        }

        woutzeemote {
            dimension 'zeemote'
        }

        withko {
            dimension 'langko'
        }

        woutko {
            dimension 'langko'
        }
    }
}

dependencies {
    compile project(':libraries:holoeverywhere')
    compile project(':libraries:holoeverywhere-preferences')
    compile fileTree(include: ['*.jar'], dir: 'src/main/libs')
    withgplayCompile fileTree(dir: 'src/withgplay/libs', include: ['*.jar'])
    withzeemoteCompile fileTree(dir: 'src/withzeemote/libs', include: ['*.jar'])
}
